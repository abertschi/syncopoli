apply plugin: 'com.android.application'

android {
    ndkVersion '25.1.8937393'
    compileSdkVersion 28

    defaultConfig {
        applicationId "org.amoradi.syncopoli"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 24
        versionName "v0.6"
        vectorDrawables.useSupportLibrary = true

        externalNativeBuild {
            ndkBuild {
                targets 'ssh', 'rsync'
                 arguments 'NDK_LOG=1'
            }
        }

        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'arm64-v8a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

gradle.taskGraph.afterTask { task ->
	if (task.name == "externalNativeBuildRelease" || task.name == "externalNativeBuildDebug") {
		def mode = "release"
		if (task.name == "externalNativeBuildDebug") {
			mode = "debug"
		}
		def src = rootProject.file("app/build/intermediates/ndkBuild/${mode}/obj/local/")
		def dst = rootProject.file('app/src/main/assets/')

		copy {
			from(src) {
				exclude "**/objs"
				exclude "**/objs-debug"
				exclude "**/*.a"
				exclude "**/*.o"
				exclude "**/*.d"
			}
			
			into dst

			eachFile {
				println "copying " + it.getPath() + " to assets directory"
			}
		}

	}
}

clean {
	delete '.externalNativeBuild'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
    }
}
		
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
}
